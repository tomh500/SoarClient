buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.guardsquare:proguard-gradle:7.6.1'
    }
}

plugins {
	id 'fabric-loom' version '1.9-SNAPSHOT'
}

project.ext.lwjglVersion = "3.3.3"
version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

loom {
	accessWidenerPath = file("src/main/resources/soar.accesswidener")
}

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
	maven { url "https://api.modrinth.com/maven" }
	maven { url "https://maven.lenni0451.net/everything" }
	maven { url "https://repo.viaversion.com/" }
	maven { url "https://repo.opencollab.dev/maven-snapshots/" }
	maven { url "https://maven.terraformersmc.com/" }
}

configurations {
    jij
	modJij
	include.extendsFrom modJij
    modImplementation.extendsFrom modJij
    modCompileOnlyApi.extendsFrom modJij
	proguard
}

dependencies {

	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation "com.viaversion:viafabricplus-api:4.0.2"

	modRuntimeOnly "maven.modrinth:sodium:mc1.21.4-0.6.6-fabric"
	modRuntimeOnly "maven.modrinth:iris:1.8.5+1.21.4-fabric"
	modRuntimeOnly "maven.modrinth:sodium-extra:mc1.21.4-0.6.1+fabric"
	modRuntimeOnly "maven.modrinth:lithium:mc1.21.4-0.14.7-fabric"
	modRuntimeOnly "maven.modrinth:in-game-account-switcher:WBbjirJP"
	modRuntimeOnly "com.viaversion:viafabricplus:4.0.2"
	modRuntimeOnly "maven.modrinth:immediatelyfast:1.3.3+1.21.4-fabric"
	modRuntimeOnly "maven.modrinth:entityculling:NkBXk0Ye"

	jij 'com.github.ben-manes.caffeine:caffeine:3.1.8'
	jij 'io.github.humbleui:types:0.2.0'
	jij 'io.github.humbleui:skija-windows-x64:0.116.4'
	jij 'io.github.humbleui:skija-linux-x64:0.116.4'
	jij 'io.github.humbleui:skija-macos-x64:0.116.4'
	jij 'io.github.humbleui:skija-macos-arm64:0.116.4'
	jij 'com.kohlschutter.junixsocket:junixsocket-common:2.10.1'
	jij 'org.java-websocket:Java-WebSocket:1.6.0'
	jij "net.lenni0451:MCPing:1.4.2"
	jij "net.lenni0451:Reflect:1.4.0"
	
	modJij 'com.github.CCBlueX:mcef:3.0.0-1.21.4'
	
	runtimeOnly "org.lwjgl:lwjgl-nfd:$lwjglVersion"
    runtimeOnly "org.lwjgl:lwjgl-nfd::natives-linux"
    runtimeOnly "org.lwjgl:lwjgl-nfd::natives-macos"
    runtimeOnly "org.lwjgl:lwjgl-nfd::natives-macos-arm64"
    runtimeOnly "org.lwjgl:lwjgl-nfd::natives-windows"

	jij "org.lwjgl:lwjgl-nfd:$lwjglVersion"
    jij "org.lwjgl:lwjgl-nfd::natives-linux"
    jij "org.lwjgl:lwjgl-nfd::natives-macos"
    jij "org.lwjgl:lwjgl-nfd::natives-macos-arm64"
    jij "org.lwjgl:lwjgl-nfd::natives-windows"

	proguard 'com.guardsquare:proguard-base:7.6.1'

	afterEvaluate {
        configurations.jij.incoming.resolutionResult.allDependencies.each {
            dependencies.include(dependencies.implementation(dependencies.compileOnlyApi(it.requested.toString()) {
                transitive = false
            }))
        }
    }
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

task proguard(type: proguard.gradle.ProGuardTask) {
    
    injars "build/libs/${project.archives_base_name}-${version}.jar"
    outjars "build/libs/${project.archives_base_name}-${version}-obfuscated.jar"
    
	fileTree("${System.properties['java.home']}\\jmods\\") {
		forEach() {
			libraryjars "${it.path}"
		}
	}
    
    dontwarn '**'
    dontnote '**'

    keepattributes 'Signature,LineNumberTable,*Annotation*,Exceptions'
    
    keep 'class com.soarclient.mixin.** { *; }'
    keeppackagenames 'com.soarclient.mixin.**'
    
    renamesourcefileattribute 'SourceFile'
	repackageclasses ''
    
    useuniqueclassmembernames
    dontusemixedcaseclassnames
    allowaccessmodification
	dontoptimize
	dontshrink

    keepattributes 'RuntimeVisible*Annotations'
    keepattributes 'RuntimeInvisible*Annotations'
}

build.finalizedBy(proguard)